#!/usr/bin/perl

=head1 Noledge

Generate wiki-like HTML pages with LaTex and GraphViz.

=cut



# Imports
use warnings;
use strict;
use Getopt::ArgParse;
use File::Find;
use File::Copy;
use File::Path 'make_path';
use File::Basename;
use File::Spec::Functions 'catfile', 'rel2abs';
use Cwd 'realpath', 'getcwd';
use lib dirname(realpath($0));
use Parallel::ForkManager;
use MovePaths;

# Globals
my $processpool;



# Command line args
my $parser = Getopt::ArgParse->new_parser(
	prog => "noledge",
	description => "Structure knowledge using LaTex, GraphViz and HTML.",
);
my $subparsers = $parser->add_subparsers(title => "command");

# Build
my $build_parser = $subparsers->add_parser(
	"build",
	help => "HTML entries",
	description => "Build all entries found in a given root directory.\n" .
	"Entry is defined as a directory containing at least a content.tex and a graph.gv file.\n"
);
$build_parser->add_arg(
	"root",
	help => "All entries found in this directory will be build."
);
$build_parser->add_arg(
	"--jobs",
	"-j",
	default => 1,
	help => "How many processes to use for building."
);
$build_parser->add_arg(
	"--quick",
	type => "Bool",
	default => 0,
	help => "If set, latex will be run once only, e.g. references might be wrong."
);

# Move
my $move_parser = $subparsers->add_parser(
	"move",
	help => "entry with automatic path adjustment.",
	description => "Move a single entry to another location." .
	"Paths in all relevant files (tex, gv, html, map) are automatically adjusted " .
	"in all entries found in a given root directory. " .
	"Note that only links on entry.html files are adjusted. " .
	"Thus, it is advisable to put for example images to an absolute path."
);
$move_parser->add_arg(
	"source",
	help => "Directory containing at least a content.tex and graph.gv file."
);
$move_parser->add_arg(
	"target",
	help => "New name of the entry's directory."
);
$move_parser->add_arg(
	"root",
	help => "Paths identified in *.tex, *.gv, .*map, and *.html files found in " .
	"this directory will be adjusted accordingly."
);

# Find
my $find_parser = $subparsers->add_parser(
	"find",
	help => "Not implemented: entries that match all keywords."
);

# Initialize entry
my $init_parser = $subparsers->add_parser(
	"init",
	help => "Set up an empty entry."
);
$init_parser->add_arg(
	"directory",
	metavar => "DIR",
	help => "Where to set up the new entry."
);



# Parse cmd line arguments
# Help
my $cmdlineargs = $parser->parse_args();
unless (defined($cmdlineargs->current_command())) {
	$parser->print_usage();
	die;
}

# All the others
if ($cmdlineargs->current_command() eq "build") {
	# Build
	# Is directory?
	(defined $cmdlineargs->root and -d $cmdlineargs->root) or die "Argument must be a directory";

	# Walk directory and build entries
	if ($cmdlineargs->jobs > 1) {
		$processpool = Parallel::ForkManager->new($cmdlineargs->jobs);
		my $subroutine = sub {
			my $entry = $File::Find::name;
			return unless is_entry($entry);

			# Build entry in forked process
			$processpool->start($entry) and return;
			build_entry($entry);
			$processpool->finish();
		};
		$processpool->run_on_finish(\&forked_build_was_successful);
		find($subroutine, ( rel2abs($cmdlineargs->root) ));
		$processpool->wait_all_children();
	}
	elsif ($cmdlineargs->jobs == 1) {
		my $subroutine = sub {
			my $entry = $File::Find::name;
			return unless is_entry($entry);
			build_entry($entry);
		};
		find($subroutine, ( rel2abs($cmdlineargs->root) ));
	}
	else {
		die "Number of jobs must be positive"
	}

}
elsif ($cmdlineargs->current_command() eq "move") {
	# Move
	unless (-d $cmdlineargs->source and ! -e $cmdlineargs->target and -d $cmdlineargs->root) {
		die "Source must be directory and target must be non-existent";
	}
	move_entry(rel2abs($cmdlineargs->source), rel2abs($cmdlineargs->target), rel2abs($cmdlineargs->root));
}
elsif ($cmdlineargs->current_command() eq "find") {
	# Find
	die "Not implemented";
}
elsif ($cmdlineargs->current_command() eq "init") {
	# Initialise
	initialise_entry($cmdlineargs->directory);
}



# Subroutines
=head1 Subroutines

=over

=item is_entry(directory)

Checks for existance of content.tex and graph.gv and
returns one if present.

=cut

sub is_entry {
	my $entry = shift;

	# Is valid directory?
	return 0 unless (-d $entry and substr($entry, 0, 1) ne ".");

	# Check if directory contains required files
	return 0 unless (-f catfile($entry, "content.tex") and -f catfile($entry, "graph.gv"));

	return 1;
}


=item forked_build_was_successful(pid, exit_code, entry)

Check if build done in parallel was successful and
print helpful error message if not.

=cut

sub forked_build_was_successful {
	my ($pid, $exit_code, $entry) = @_;
	$exit_code and die "Build of $entry was not successful, exit code $exit_code";
}



=item build_entry(directory)

Build noledge entry:

=over 8

=item 1)
If non-existant, write default entry.css.

=item 2)
If necessary, build content.svg and graph.gv (via timestamps).

=item 3)
Repair some stuff (HTML title, LaTex hyperref links).

=item 4)
Generate entry.html.

=back

=cut

sub build_entry {

	my $directory = shift;

	-d $directory or die "Argument must be a directory";
	my $wd = getcwd();
	chdir($directory) or die "Could not cd to $directory";

	(-f "content.tex" and -f "graph.gv") or
	die "content.tex or graph.gv missing";

	# Defines
	my $msg = "Error processing \"$directory\":";

	# Create build dir
	if (! -d "build") { mkdir("build"); }

	# Create css file if non-existant
	# Note that the user can define a custom one, it won't be overwritten
	if (! -e "entry.css") {
		open(FH, ">", "entry.css") or die "$msg Could not open entry.css for writing";
		print(FH
			".graph {\n" .
			"	display: block;\n" .
			"	margin-right: auto;\n" .
			"	margin-left: auto;\n" .
			"	margin-top: 2cm;\n" .
			"	margin-bottom: 2.5cm;\n" .
			"}\n" .
			".content {\n" .
			"	display: block;\n" .
			"	margin-right: auto;\n" .
			"	margin-left: auto;\n" .
			"	margin-top: 0cm;\n" .
			"	margin-bottom: 2cm;\n" .
			"	shape-rendering: geoemtricPrecision;\n" .
			"}\n"
		);
		close(FH);
	}

	# Check what was changed; for later use
	my $graph_changed;
	{
		my $age = -M "graph.gv";
		$graph_changed = !(
			(-f "build/graph.svg" and $age >= -M "build/graph.svg") and
			(-f "build/graph.map" and $age >= -M "build/graph.map")
		);
	}
	my $content_changed;
	{
		my $age = -M "content.tex";
		$content_changed = !(-f "build/content.svg" and $age >= -M "build/content.svg");
	}

	# Check if something must be done
	unless ((! -e "entry.html") or $content_changed or $graph_changed) {
		chdir($wd) or die "Could not cd to $wd";
		return;
	}

	# Compile graph
	if ($graph_changed) {
		if (
		system("dot -Tsvg graph.gv -o build/graph.svg") or
		system("dot -Tcmapx graph.gv -o build/graph.map")
		) {
			die "$msg Could not compile \"graph.gv\".";
		}
	}
	# Read code of graph map for later integration into the html
	my $htmlgraph;
	{
		my $graphmap;
		open(FH, "<", "build/graph.map") or die "$msg Could not open graph map file";
		read(FH, $graphmap, -s FH);
		close(FH);
		$graphmap =~ s/\s+$//; # Strip right
		$htmlgraph =
			"<IMG SRC=\"build/graph.svg\" USEMAP=\"\#G\" class=\"graph\">\n$graphmap";
	}

	# Get title of the entry from tex code
	my $title;
	{
		my $tex;
		open(FH, '<', 'content.tex') or die "$msg Could not open content.tex $_";
		read(FH, $tex, -s FH);
		close(FH);
		$tex =~ /\\title\{(.*?)\}/;
		$title = $1;
		$title = "" unless defined($title);
	}

	# Compile tex
	my $htmlcode;
	if ($content_changed) {
		# How often to run latex?
		my $runs = 2;
		if ($cmdlineargs->quick) { $runs = 1; }
		# Run latex
		if (system(
			#--no-shell-escape
			('latex --output-format=dvi --output-dir=build content.tex && ' x $runs) . # TODO: Sure that is required?
			'dvisvgm --bbox=min --exact --page=2 --linkmark=none -n build/content.dvi --output=build/%f.svg'
		)) { die "$msg Could not compile \"content.tex\""; }
	}

	# Read compiled content for later integration into html
	my $htmlcontent;
	{
		open(FH, "<", 'build/content.svg') or die "$msg Could not open content.svg $_";
		read(FH, $htmlcontent, -s FH);
		close(FH);
		# Insert class to center content
		my $keyword = "<svg ";
		my $i = index($htmlcontent, $keyword);
		substr($htmlcontent, $i + length($keyword), 0) = 'class="content" ';
	}

	# Repair latex hyperref links by adding dividers
	{
		# Get all views (where hyperref links lead to)
		my @svg_views;
		my $svg_view_pattern = "<view id='loc([0-9]+)' viewBox='[0-9.\\s]+? ([0-9.]+?) [0-9.\\s]+'\\/>";
		while ($htmlcontent =~ /$svg_view_pattern/g) {
			push @svg_views, "<div style='position: absolute; top: $2px; left: 0px'><a id='loc$1'></a></div>";
			substr($htmlcontent, $-[0], $+[0] - $-[0]+1) = ''; # Assumes newline
		}
		$htmlcontent = join("\n", $htmlcontent, @svg_views);
	}

	# Write html to file (here it is known that graph or content changed
	open(FH, ">", "entry.html") or die "$msg Could not write html file";
	print(FH
		"<!DOCTYPE html>\n" .
		"<html lang=\"en-US\">\n" .
		"<head>\n" .
		"<title>$title</title>\n" .
		"<meta charset=\"utf-8\" />\n" .
		"<meta name=\"viewport\" content=\"width=device-width,initial-scale=1\" />\n" .
		"<link rel=\"stylesheet\" type=\"text/css\" href=\"entry.css\" />\n" .
		"</head>\n" .
		"<body>\n" .
		$htmlgraph .
		"\n".
		$htmlcontent .
		"\n</body>\n".
		"</html>\n"
	);
	close(FH);
	# Go back to working directory
	chdir($wd) or die "Could not cd to $wd";
}



=item move_entry(source, target, root)

Use the MovePaths package to move a noledge entry.

=cut
sub move_entry {
	# Get arguments
	my ($source, $target, $root) = @_;
	$source = realpath($source);
	$target = realpath($target);
	$root = realpath($root);

	find(MovePaths::subroutine_for_find("entry\\.html", $source, $target, \&separators), ($root));
	make_path(dirname($target));
	move($source, $target) or die "Could not move entry $source";
}

=item separators(name)

Used in move_entry() for identifying relevant files (with paths that need updating) and
selecting delimiters of paths (e.g. {} in LaTex).

=cut
sub separators {
	# Get extension
	my $ext;
	{
		my $name = shift;
		$name =~ /\.([^.]+)$/;
		$ext = $1;
		unless (defined($ext)) { return; }
	}

	# Select start and end delimiters
	my $start;
	my $end;
	if ($ext eq "tex") {
		$start = "\{";
		$end = "\}";
	}
	elsif ($ext =~ /(gv|map|html)/) {
		$start = "\"";
		$end = "\"";
	}
	elsif ($ext =~ /(svg|css)/) {
		system("touch ${File::Find::name}");
	}
	else {
		return;
	}
	return ($start, $end);
}


=item initialise_entry(directory)

Generates directory with default tex and gv files, convenience.

=cut
sub initialise_entry {

	my $dir = shift;

	die "$dir already exists" if (-e $dir);
	die "Parent directory of $dir must exist" if (! -e dirname($dir));

	mkdir($dir);

	open(FH, ">", "$dir/content.tex");
	print(FH
		"\\documentclass{article}\n" .
		"\\usepackage[hypertex, colorlinks=true, urlcolor=blue]{hyperref}\n" .
		"\\usepackage[dvisvgm]{graphicx}\n" .
		"\\usepackage[margin=1in]{geometry}\n" .
		"\\title{}\n" .
		"\\author{}\n" .
		"\\date{}\n\n" .
		"\\begin{document}\n" .
		"\\pagenumbering{gobble}\n" .
		"\\vbox{\n" .
		"\\maketitle\n\n" .
		"}\n" .
		"\\end{document}\n\n"
	);
	close(FH);

	open(FH, ">", "$dir/graph.gv");
	print(FH "digraph G {\n\n}\n\n");
	close(FH);
}

=back
=cut

